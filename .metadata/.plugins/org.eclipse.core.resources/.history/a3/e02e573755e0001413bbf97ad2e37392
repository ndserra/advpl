#Include 'Protheus.ch'
#Include 'Protheus.ch'
#INCLUDE 'TOPCONN.CH'

User Function Treport_Simples()
	Local oReport := nil
	Local cPerg:= "XRELPROD"
	
	AjustaSX1(cPerg)

	Pergunte(cPerg,.F.)
		
	oReport := RptDef(cPerg)
	
	oReport:PrintDialog()
Return()
//------------------------------------------------------------------------------------------------

Static Function RptDef(cNome)
	Local oReport := Nil
	Local oSection1:= Nil
	Local oBreak
	Local oFunction
	
	/*Sintaxe: TReport():New(cNome,cTitulo,cPerguntas,bBlocoCodigo,cDescricao)*/
	oReport := TReport():New(cNome,"Cadastro Produtos",cNome,{|oReport| ReportPrint(oReport)},"Descrição do meu relatório")
	// Relatorio em retrato 
	oReport:SetPortrait()
	oReport:SetTotalInLine(.F.)
	
	//Monstando a primeira seção

	oSection1:= TRSection():New(oReport, "Produtos", {"SB1"}, NIL, .F., .T.)
	TRCell():New(oSection1,"B1_COD"   	,"SB1","Produto"		,"@!",30)
	TRCell():New(oSection1,"B1_DESC"  	,"SB1","Descrição"	,"@!",100)
	TRCell():New(oSection1,"B1_LOCPAD"	,"SB1","Arm.Padrao"	,"@!",20)
	TRCell():New(oSection1,"B1_POSIPI"	,"SB1","NCM"			,"@!",30)

	//O parâmetro que indica qual célula o totalizador se refere ,
	//será utilizado para posicionamento de impressão do totalizador quando 
	//estiver definido que a impressão será por coluna e como conteúdo para a 
	//função definida caso não seja informada uma fórmula para o totalizador
	TRFunction():New(oSection1:Cell("B1_COD"),NIL,"COUNT",,,,,.F.,.T.)
	
	
Return(oReport)

Static Function ReportPrint(oReport)
	Local oSection1 := oReport:Section(1)
	Local cQuery    := ""
	Local cNcm      := ""
	Local lPrim 	:= .T.
	Local cAlias  := GetNextAlias()
	
	cPart := "% AND B1_COD >= '" + MV_PAR01 + "' "
	cPart += "  AND B1_COD <= '" + MV_PAR02 + "' %"
	
  //cPart := "%%"
  
	BeginSql alias cAlias

		SELECT B1_COD,B1_DESC,B1_LOCPAD,B1_POSIPI
		FROM %table:SB1% SB1
		WHERE B1_FILIAL = %xfilial:SB1%
		AND B1_MSBLQL <> '1'
		AND SB1.%notDel%
	
		%exp:cPart%
	
		ORDER BY B1_COD
	
	EndSql
		
	dbSelectArea(cAlias)
	(cAlias)->(dbGoTop())
	
	oReport:SetMeter((cAlias)->(LastRec()))

	//todos os meus registros
	While !(cAlias)->( EOF() )
		
		If oReport:Cancel()
			Exit
		EndIf
	
		oReport:IncMeter()
			
		IncProc("Imprimindo " + alltrim((cAlias)->B1_DESC))
		
		//inicializo a primeira seção
		
		oSection1:Init()
	
		//imprimo a seção				
			
		oSection1:Cell("B1_COD"   ):SetValue((cAlias)->B1_COD    )
		oSection1:Cell("B1_DESC"  ):SetValue((cAlias)->B1_DESC   )
		oSection1:Cell("B1_LOCPAD"):SetValue((cAlias)->B1_LOCPAD )
		oSection1:Cell("B1_POSIPI"):SetValue((cAlias)->B1_POSIPI)
		oSection1:Printline()
	
		(cAlias)->(dbSkip())
 			
		//imprimo uma linha 
		oReport:ThinLine()
		
	Enddo
	
	//finalizo seção
	oSection1:Finish()
	
Return( NIL )

//--------------------------------------------------------------------------------------------------------
Static Function ajustaSx1(cPerg)
	//Aqui utilizo a função putSx1, ela cria a pergunta na tabela de perguntas
	putSx1(cPerg, "01", "PRODUTO DE ?"	  , "", "", "mv_ch1", "C", tamSx3("B1_COD")[1], 0, 0, "G", "", "SB1", "", "", "mv_par01")
	putSx1(cPerg, "02", "PRODUTO ATE?"	  , "", "", "mv_ch2", "C", tamSx3("B1_COD")[1], 0, 0, "G", "", "SB1", "", "", "mv_par02")
return