#INCLUDE "FIVEWIN.CH"
#INCLUDE "RGPER07.CH"
#INCLUDE "REPORT.CH"
#INCLUDE "PROTHEUS.CH"

/*


Ŀ
Funo     RGPER07   Autor  Silvia Taguti          Data  30.04.03 
Ĵ
Descrio  Relacao de Entrada de Funcionarios                         
Ĵ
Sintaxe    RGPER07(void)                                              
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ
 		ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.		      
Ĵ
Programador  Data      BOPS Motivo da Alteracao					  
Ĵ
Tatiane     09/08/2006100725|Conversao para relatorio personalizavel  
Tatiane V.M.25/08/2009020388|Compatibilizacao dos fontes para aumento
                        2009| do campo filial e gesto corporativa.  
Francisco Jr05/11/2009026616|Compatibilizacao dos fontes para aumento
                        2009| do campo filial e gesto corporativa.  
                            |otimizar  Atusx1.                       
ٱ

*/
User Function RGPER07() 

Local oReport 

Private cAliasMov		:= ""       

	If FindFunction("TRepInUse") .And. TRepInUse()                         
	
		
		//-- Interface de impressao 
		
		Pergunte("RGP07R",.F.)
	    oReport := ReportDef()
		oReport:SetLandScape()
   		oReport:PrintDialog()
 	Else
 		RGPER07R3()
	EndIF    

Return
/*


Ŀ
Funo     ReportDef Autor  R.H. - Tatiane Matias  Data  09.08.06 
Ĵ
Descrio  Definicao do relatorio                                     
ٱ

*/
Static Function ReportDef()     

//-- Objeto Relatorio
Local oReport  

//-- Objeto Section
Local oSection1   
//-- Descricao do Relatorio
Local	cDesc		:=	STR0004 + STR0005 + STR0006 //"Relao de Cargos e Salario."#"Ser impresso de acordo com os parametros solicitados pelo" # "usuario."

//-- Ordens utilizadas pelo relatorio
Local aOrd	:= {	STR0001,;				//"C.Custo + Matricula "
						STR0002 }				//"C.Custo + Nome"

	//-- Inicio definicao do Relatorio
	DEFINE REPORT oReport NAME "RGPER07" TITLE STR0009 PARAMETER "RGP07R" ACTION {|oReport| PrintReport(oReport)} DESCRIPTION cDesc TOTAL IN COLUMN
	
		//-- Section de Centro de Custo
		DEFINE SECTION oSection1 OF oReport TABLES "SRA" TITLE STR0019 ORDERS aOrd  TOTAL IN COLUMN
		oSection1:SetHeaderBreak(.T.)
				
			DEFINE CELL NAME "RA_CC"	OF oSection1 ALIAS "SRA" BLOCK {|| DescCc((cAliasMov)->RA_CC,(cAliasMov)->RA_FILIAL) } SIZE 30

			//-- Section de Funcionario
			DEFINE SECTION oSection2 OF oSection1 TABLES "SRA" TITLE STR0013 TOTAL IN COLUMN
			oSection2:SetHeaderBreak(.T.)
		
				DEFINE CELL NAME "RA_MAT"		OF oSection2 ALIAS "SRA"
				DEFINE CELL NAME "RA_NOME"		OF oSection2 ALIAS "SRA"
				DEFINE CELL NAME "RA_CODFUNC"	OF oSection2 ALIAS "SRA" BLOCK {|| DESCFUN((cAliasMov)->RA_CODFUNC,(cAliasMov)->RA_FILIAL) } SIZE 20
				DEFINE CELL NAME "RA_ADMISSA"	OF oSection2 ALIAS "SRA"
				DEFINE CELL NAME "RA_DTENTRA"	OF oSection2 ALIAS "SRA"
				DEFINE CELL NAME "RA_SALARIO"	OF oSection2 ALIAS "SRA"

				//-- Total do Centro de Custo e Total da Empresa
				DEFINE FUNCTION FROM oSection2:Cell("RA_MAT") 		FUNCTION COUNT OF oSection1
				DEFINE FUNCTION FROM oSection2:Cell("RA_SALARIO") 	FUNCTION SUM 	OF oSection1
				
Return oReport

/*


Ŀ
Funo    PrintReport Autor  R.H. - Tatiane Matias    Data  13.07.06 
Ĵ
Descrio  Valores de Rescisao - Relatorio Personalizavel                
ٱ

*/
Static Function PrintReport(oReport)     

//Ŀ
// Declaracao de Variaveis Locais                                
//
//-- Numerico
Local nOrdem   		:= oReport:Section(1):GetOrder()
Local nCont				:= 0

//-- Objeto                
Local oSection1		:= oReport:Section(1)
Local oSection2		:= oSection1:Section(1)

//Ŀ
// Declaracao de Variaveis Privates                             
//
//-- Array
Private aCodFol		:= {}
Private aInfo			:= {}
Private aRoteiro  	:= {}

//-- String
Private cAcessaSRA 	:= &("{ || " + ChkRH("RGPER07","SRA","2") + "}")
Private cFilaInfo		:= ""

//--Numerico
Private nSalario   	:= nSalMes := nSalDia := nSalHora := 0

	cSituacao    := mv_par05									//  Situacao Funcionario
	cCategoria   := mv_par06									//  Categoria Funcionario

	//Ŀ
	// Faz filtro no arquivo...                                                 
	//
	#IFDEF TOP     
		cAliasMov 	:= GetNextAlias()

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeSqlExpr("RGP07R")

		//Ŀ
		// Modifica variaveis para a Query                                          
		//
		//-- Situacao do Funcionario
		cSitQuery := ""
		
		For nCont:=1 to Len(cSituacao)
			cSitQuery += "'"+Subs(cSituacao,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cSituacao)
				cSitQuery += "," 
			Endif
		Next nCont     
		cSitQuery := "%" + cSitQuery + "%"
		

		//-- Categoria do Funcionario
		cCatQuery := ""
		For nCont:=1 to Len(cCategoria)
			cCatQuery += "'"+Subs(cCategoria,nCont,1)+"'"
			If ( nCont+1 ) <= Len(cCategoria)
				cCatQuery += "," 
			Endif
		Next nCont
		cCatQuery := "%" + cCatQuery + "%"

		If nOrdem == 1										// Centro de Custo + Matricula
			cOrdem := "%RA_FILIAL, RA_CC, RA_MAT%"
		Else													// Centro de Custo + Nome
			cOrdem := "%RA_FILIAL, RA_CC, RA_NOME%"
		EndIf

		//Ŀ
		// Inicializa Query                                                  		  
		//
		BEGIN REPORT QUERY oSection1

		BeginSql alias cAliasMov

			SELECT RA_FILIAL, RA_MAT, RA_NOME, RA_CODFUNC, RA_ADMISSA, RA_DTENTRA, RA_SALARIO, RA_CC
			FROM %table:SRA% SRA
			WHERE SRA.RA_SITFOLH IN (%exp:Upper(cSitQuery)%) 	AND
				   SRA.RA_CATFUNC IN (%exp:Upper(cCatQuery)%) 	AND
	  				SRA.%notDel%
			ORDER BY %exp:cOrdem%                                                            
			
		EndSql
	
		/*
		Prepara relatorio para executar a query gerada pelo Embedded SQL passando como 
		parametro a pergunta ou vetor com perguntas do tipo Range que foram alterados 
		pela funcao MakeSqlExpr para serem adicionados a query
		*/
		END REPORT QUERY oSection1 PARAM mv_par01, mv_par02, mv_par03, mv_par04, mv_par07

		//-- Utiliza a query do Pai (Section2) - Funcionarios
		oSection2:SetParentQuery()
		oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_CC })

	#ELSE             
		cAliasMov 	:= "SRA"     

		//Transforma parametros do tipo Range em expressao ADVPL para ser utilizada no filtro
		MakeAdvplExpr("RGP07R")

		dbSelectArea(cAliasMov)
		
		cFiltro := ""
		//-- Adiciona no filtro o parametro tipo Range
		//-- Filial
		If !Empty(mv_par01)
			cFiltro += mv_par01 + " .AND. "
		EndIf                 

		//-- Centro de Custo
		If !Empty(mv_par02)
			cFiltro += mv_par02 + " .AND. "
		EndIf                 

		//-- Matricula
		If !Empty(mv_par03)
			cFiltro += mv_par03 + " .AND. "
		EndIf                 

		//-- Nome
		If !Empty(mv_par04)
			cFiltro += mv_par04 + " .AND. "
		EndIf                 

		//-- Data
		If !Empty(mv_par07)
			cFiltro += mv_par07 + " .AND. "
		EndIf                 

	  	cFiltro += 'RA_SITFOLH $ "' + cSituacao + '" .AND. '
		cFiltro += 'RA_CATFUNC $ "' + cCategoria + '"'      

		//-- Filtra Section de Centro de CUsto
		oSection1:SetFilter(cFiltro)

		//-- Filtra Section Filha - Funcionario
		oSection2:SetFilter(cFiltro)
		oSection2:SetParentFilter({|cParam| (cAliasMov)->RA_CC == cParam},{|| (cAliasMov)->RA_CC})

		If nOrdem == 1
			oSection1:SetIdxOrder ( 2 ) 
			oSection2:SetIdxOrder ( 2 ) 
		ElseIf nOrdem == 2					//Turno de Trabalho
			oSection1:SetIdxOrder ( 8 ) 
			oSection2:SetIdxOrder ( 8 ) 
		EndIf
                         
	#Endif	 

	//-- Define o total da regua da tela de processamento do relatorio
	oReport:SetMeter((cAliasMov)->( RecCount() ))  

	//-- Condicao para Impressao das Sections
	oSection1:SetLineCondition({|| fCondSRA() })

	//-- Rodape
	oReport:SetPageFooter(2,{|| oReport:PrintText(SPACE(15)+Repli("_",25)+SPACE(20)+Repli("_",25)), oReport:PrintText(SPACE(20)+STR0017+SPACE(30)+STR0018) })

	oSection1:Print()
	                  
	//Ŀ
	// Termino do relatorio													  
	//
	#IFNDEF TOP 
		dbSelectArea(cAliasMov)
		dbSetOrder(1)
		Set Filter To
	#Endif	

Return NIL

/*


Ŀ
Funo    fCondSRA    Autor  R.H. - Tatiane Matias    Data  09.08.06 
Ĵ
Descrio  Verifica se a linha deve ser impressa                         
Ĵ
Sintaxe    fCondSRA()                                                    
Ĵ
Parametros                                                               
Ĵ
 Uso       Generico                                                      
ٱ

*/
Static Function fCondSRA()

//-- Variavel de Retorno
Local lRetorno := .T.

	/*
	Ŀ
	 Verifica Quebra de Filial                                    
	*/
	If (cAliasMov)->RA_FILIAL # cFilaInfo .and. lRetorno 
		If !fp_CodFol(@aCodFol,(cAliasMov)->RA_FILIAL) .Or. !fInfo(@aInfo,(cAliasMov)->RA_FILIAL)
			//-- Registro nao deve ser impressao (Nao acessa Filial ou nao carregado os identificadores de calculo) 
			lRetorno := .F.                                    

			//-- Carrega os arrays com as informacoes da Filial anterior.
			fp_CodFol(@aCodFol,cFilaInfo)
			fInfo(@aInfo,cFilaInfo)
		Else
			cFilaInfo 		:= (cAliasMov)->RA_FILIAL
		EndIf
	EndIf

	/*
	Ŀ
	 Consiste controle de acessos e filiais validas			   |
	*/
	If !((cAliasMov)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
		//-- Registro nao deve ser impressao (Nao acessa tabela de Funcionarios ou Filial invalida) 
		lRetorno := .F.
	EndIf
	
	nSalario   := 0
	nSalMes	  := 0
	nSalDia	  := 0
	nSalHora   := 0

	//Ŀ
	// Calcula Salario Incorporado Mes , Dia , Hora do Funcionario  
	//
	fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.)
	
Return lRetorno



/*


Ŀ
Funo     RGPER07R3 Autor  Silvia Taguti          Data  30.04.03 
Ĵ
Descrio  Relacao de Entrada de Funcionarios                         
Ĵ
Sintaxe    RGPER07R3(void)                                            
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/

User Function RGPER07R3()
//Ŀ
// Define Variaveis Locais (Basicas)                            
//
Local cString := "SRA"        // alias do arquivo principal (Base)
Local aOrd    := {STR0001,STR0002,OemtoAnsi(STR0003),STR0012,STR0013,OemtoAnsi(STR0014)}    //"C.Custo + Matricula "###"C.Custo + Nome"###"C.Custo + Funo"###"Nome"###"Matricula"###"Funo"
Local cDesc1  := STR0004		//"Relao de Cargos e Salario."
Local cDesc2  := STR0005		//"Ser impresso de acordo com os parametros solicitados pelo"
Local cDesc3  := STR0006		//"usuario."

//Ŀ
// Define Variaveis Private(Basicas)                            
//
Private aReturn  := {STR0007,1,STR0008,2,2,1,"",1 }	//"Zebrado"###"Administrao"
Private NomeProg := "RGPER07"
Private aLinha   := { }
Private nLastKey := 0
Private cPerg    := "RGPR07"

//Ŀ
// Variaveis Utilizadas na funcao IMPR                          
//
Private Titulo   := STR0009		//"RELAO DE CARGOS E SALARIOS"
Private AT_PRG   := "RGPER07"
Private Wcabec0  := 2
Private cabec1  := STR0010		//"FI  MATRIC NOME                           ADMISSAO   FUNCAO                  MAO DE       SALARIO   PERC.   PERC.   PERC."
Private Cabec2 := ""
Private CONTFL   := 1
Private LI		  := 0
Private nTamanho := "M"
Private cPict1	:=	If (MsDecimais(1)==2,"@E 999,999,999,999.99",TM(999999999999,18,MsDecimais(1)))  // "@E 999,999,999,999.99
Private cPict2	:=	If (MsDecimais(1)==2,"@E 999,999,999.99",TM(999999999,14,MsDecimais(1)))  // "@E 999,999,999.99
Private m_pag      := 01


Pergunte("RGPR07",.F.)

//CODIGO     NOMBRE                         CARGO     INGRESO        ENTRADA      SALARIO    ACCION
//0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456

//Ŀ
// Envia controle para a funcao SETPRINT                        
//
wnrel:="RGPER07"            //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,nTamanho)

If nLastKey = 27
	Return
EndIf

SetDefault(aReturn,cString)

If nLastKey = 27
	Return
EndIf

RptStatus({|lEnd| RGPR07Imp(@lEnd,wnRel,cString)},Titulo)

Return

/*

Ŀ
Funo     RGPR07IMP Autor  R.H.                   Data  15.04.96 
Ĵ
Descrio  Impressao da Relacao de Empleados                          
Ĵ
Sintaxe    RGPR07IMP(lEnd,WnRel,cString)                              
Ĵ
 Uso       Generico                                                   
ٱ

*/
Static Function RGPR07IMP(lEnd,WnRel,cString)
//Ŀ
// Define Variaveis Locais (Programa)                           
//
Local nSalario   	:= nSalMes := nSalDia := nSalHora := 0
Local nTOTCC      	:= 0 //Alterado o Tipo de Array para Numerico
Local nTotFunc	  	:= 0
Local nTotFunGer	:= 0
Local nTotSalGer   := 0
Local cAcessaSRA := &("{ || " + ChkRH("RGPER07","SRA","2") + "}")

//Ŀ
// Define Variaveis Private(Programa)                           
//
Private aInfo     := {}
Private aCodFol   := {}
Private aRoteiro  := {}

//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01        //  Filial De                                
// mv_par02        //  Filial Ate                               
// mv_par03        //  Centro de Custo De                       
// mv_par04        //  Centro de Custo Ate                      
// mv_par05        //  Matricula De                             
// mv_par06        //  Matricula Ate                            
// mv_par07        //  Nome De                                  
// mv_par08        //  Nome Ate                                 
// mv_par09        //  Situaes                                
// mv_par10        //  Categorias                               
// mv_par11        //  Data Pagamento De                        
// mv_par12        //  Data Pagamento Ate                       
//

//Ŀ
// Carregando variaveis mv_par?? para Variaveis do Sistema.	 
//
nOrdem 		 := aReturn[8]
cFilDe	    := mv_par01									//  Filial De
cFilAte	    := mv_par02									//  Filial Ate
cCcDe 	    := mv_par03									//  Centro de Custo De
cCcAte	    := mv_par04									//  Centro de Custo Ate
cMatDe	    := mv_par05									//  Matricula De
cMatAte	    := mv_par06									//  Matricula Ate
cNomeDe	    := mv_par07									//  Nome De
cNomeAte     := mv_par08									//  Nome Ate
cSituacao    := mv_par09									//  Situacao Funcionario
cCategoria   := mv_par10									//  Categoria Funcionario
dDataDe      := mv_par11
dDataAte		 := mv_par12

If nOrdem == 1
	dbSetOrder(2)
	dbSeek(cFilDe + cCcDe + cMatDe,.T.)
	cInicio := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_MAT"
	cFim    := cFilAte + cCcAte + cMatAte
ElseIf nOrdem == 2
	dbSetOrder(8)
	dbSeek(cFilDe + cCcDe + cNomeDe,.T.)
	cInicio := "SRA->RA_FILIAL + SRA->RA_CC + SRA->RA_NOME"
	cFim    := cFilAte + cCcAte + cNomeAte
Endif
// Sempre na ordem de Centro de Custo + Matricula para totalizar
dbSelectArea( "SRA" )
dbSetOrder(2)					
dbGoTop()

//Ŀ
// Carrega Regua Processamento											  
//
SetRegua(RecCount())

cFilialAnt := Replicate("!",FWGETTAMFILIAL) 
cFANT 	  := Space(FWGETTAMFILIAL)
cCANT 	  := Space(20)
cFuncaoAnt := "    "

TPAGINA	 := TEMPRESA := TFILIAL := TCCTO := FL1 := 0
TEMPRESAF := TFILIALF := TCCTOF	:= 0

While !EOF() .And. &cInicio <= cFim 

	//Ŀ
	// Movimenta Regua Processamento										  
	//
	IncRegua()

	If lEnd
		@Prow()+1,0 PSAY cCancel
		Exit
	EndIf

	//Ŀ
	// Verifica Quebra de Filial 											  
	//
	If SRA->RA_FILIAL # cFilialAnt
		If !Fp_CodFol(@aCodFol,SRA->RA_FILIAL) 			 .Or.;
			!fInfo(@aInfo,SRA->RA_FILIAL)
			dbSelectArea("SRA")
			dbSkip()
			If Eof()
				Exit
			Endif	
			Loop
		Endif
		dbSelectArea( "SRA" )
		cFilialAnt := SRA->RA_FILIAL
	EndIf

	If ( SRA->RA_FILIAL < cFilDe )	.Or. ( SRA->RA_FILIAL > cFilAte )	.Or.;
	   ( SRA->RA_NOME < cNomeDe )	.Or. ( SRA->RA_NOME > cNomeAte )	.Or.;
		( SRA->RA_MAT < cMatDe )	.Or. ( SRA->RA_MAT > cMatAte )		.Or.;
		(SRA->RA_CC < cCcDe) 		.Or. (SRA->RA_CC > cCcAte)		
		dbSkip()
		Loop			
	EndIf
	
	//Ŀ
	// Testa Situacao do Funcionario na Folha						 
	//
	If !( SRA->RA_SITFOLH $ cSituacao ) .Or. !( SRA->RA_CATFUNC $ cCategoria )
		dbSkip()
		Loop
	EndIf

	//Ŀ
	// Consiste controle de acessos e filiais validas               
	//
	If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
		dbSkip()
		Loop
	EndIf
   
 	If SRA->RA_ADMISSA < dDataDe .or. SRA->RA_ADMISSA > dDataAte
 		dbSkip()
 		Loop
 	Endif
	nSalario   := 0
	nSalMes	  := 0
	nSalDia	  := 0
	nSalHora   := 0

	//Ŀ
	// Calcula Salario Incorporado Mes , Dia , Hora do Funcionario  
	//
	fSalInc(@nSalario,@nSalMes,@nSalHora,@nSalDia,.T.)
	dbSelectArea( "SRA" )   
	IF substr(SRA->RA_CC+space(20),1,20) # cCANT .Or. SRA->RA_FILIAL # cFANT
		If !Empty(nTOTCC) 			
			IMPR(Repli("-",132),"C")
			DET := STR0015+StrZero(nTotFunc,3)+Space(60)+Transform(nTotCC,cPict2)
			IMPR(DET,"C")
			IMPR(Repli("-",132),"C")   
			nTotCC := 0
			nTotFunc := 0
	   Endif 
	Endif 
	IF substr(SRA->RA_CC+space(20),1,20) # cCANT .Or. SRA->RA_FILIAL # cFANT
		cCAnt:= substr(SRA->RA_CC+space(20),1,20) 
		cFAnt:= SRA->RA_FILIAL
		DET := DescCc(cCAnt,cFAnt)
		IMPR(DET,"C")
 	Endif      
 	If Li >=54
		IMPR("","C")
		IMPR("","C")
 		Det:= SPACE(15)+Repli("_",25)+SPACE(20)+Repli("_",25)
		IMPR(DET,"C")
		Det:= SPACE(20)+STR0017+SPACE(30)+STR0018
		IMPR(DET,"C")
		IMPR("","C")
 	Endif
   DET := StrZero(Val(SRA->RA_MAT),6) + "   "+ SubStr(SRA->RA_NOME,1,30)+" "
   DET += DESCFUN(SRA->RA_CODFUNC,SRA->RA_FILIAL)+ "   "
   Det += PadR(DTOC(SRA->RA_ADMISSA),10)+"   "+Padr(DTOC(SRA->RA_DTENTRA),10)+"   "
	DET += Transform( SRA->RA_SALARIO,cPict2)+"   "+Repli("_",10)
	nTotcc += SRA->RA_SALARIO
	nTotFunc++
	nTotSalGer += SRA->RA_SALARIO
	nTotFunGer++
	IMPR(DET,"C")
	dbSelectArea( "SRA" )
	dbSkip()
EndDo

If !Empty(nTOTCC) 			
	IMPR(Repli("-",132),"C")
	DET := STR0015+StrZero(nTotFunc,3)+Space(60)+Transform(nTotCC,cPict2)
	IMPR(DET,"C")
	IMPR(Repli("-",132),"C")   
Endif 

If !Empty(nTotSalGer) 			
	DET := STR0016+StrZero(nTotFunGer,3)+Space(60)+Transform(nTotSalGer,cPict2)
	IMPR(DET,"C")
	IMPR(Repli("-",132),"C")   
Endif	

If Li < 54
   Li := 54
Endif   
IMPR("","C")
IMPR("","C")
Det:= SPACE(15)+Repli("_",25)+SPACE(20)+Repli("_",25)
IMPR(DET,"C")
Det:= SPACE(20)+STR0017+SPACE(30)+STR0018
IMPR(DET,"C")
IMPR("","C")

//Ŀ
// Termino do relatorio													  
//
dbSelectArea("SRA")
dbSetOrder(1)
Set Filter To

If aReturn[5] = 1
	Set Printer To
	Commit
	ourspool(wnrel)
Endif

MS_FLUSH()

Return
