#include "rwmake.ch"        
#INCLUDE "RSR001.CH"

User Function Rsr001()

SetPrvt("CDESC1,CDESC2,CDESC3,CSTRING,AORD,IMPRIME")
SetPrvt("LEND,ARETURN,NOMEPROG,ALINHA,NLASTKEY,CPERG")
SetPrvt("TITULO,AT_PRG,CCABEC,WCABEC0,WCABEC1,CONTFL")
SetPrvt("LI,NOPC,NOPCA,WNREL,NTAMANHO,NORDEM")
SetPrvt("L1VEZ,CCANCEL,CARQNTX,CINDCOND,CINICIO,CFIM")
SetPrvt("CDETALHE,CVALID,CVAR,I,CLINE,LATIV")
SetPrvt("CSAVALIAS,NSAVREG,NLINHA,CDESCFAT,CDESCGRA,CDESCGRU")

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ RSR001   ³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime os curriculos conforme parametros selecionados     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Leandro Dr. ³01/04/09|005878³Ajuste para quebrar linha quando descricao³±±
±±³            ³        |      ³do fator ou grau ultrapassar 30 caracteres³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
cDesc1  := OemtoAnsi(STR0001) //"Este programa tem como objetivo imprimir os curriculos"
cDesc2  := OemtoAnsi(STR0002) //"conforme parametros selecionados."
cDesc3  := ""
cString := "SQG"  	
aOrd    := {STR0003,STR0004} //"Curriculo"###"Nome"
Imprime := .T.
lEnd    := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis (Basicas)                            		 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aReturn  := { STR0005,1,STR0006,2,2,1,"",1 } //"Zebrado"###"Administracao"
NomeProg := "RSR001"
aLinha   := {}
nLastKey := 0
cPerg    := "RSR001"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis (Programa)                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Titulo  := OemToAnsi(STR0007) //"Curriculos"
At_prg  := "RSR001"
cCabec  := ""
wCabec0 := 0
wCabec1 := ""
ContFl  := 1
Li      := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
pergunte("RSR001",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01        //  Curriculo de                             ³
//³ mv_par02        //  Curriculo ate                            ³
//³ mv_par03        //  Nome de              			         ³
//³ mv_par04        //  Nome ate        						 ³
//³ mv_par05        //  Area de					        		 ³
//³ mv_par06        //  Area ate                      		     ³
//³ mv_par07        //  Situacao         			  			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpc:=2
nOpca:=2
wnrel:="RSR001"   					    //Nome Default do relatorio em Disco
wnrel:=SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd)

If nLastKey == 27
	Return 
EndIf

SetDefault(aReturn,cString)

If nLastKey == 27
	Return 
EndIf

RptStatus({|| F001Impr()})

Return 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ F001Impr ³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de impressao dos curriculos                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function F001Impr()
cCabec  := ""
At_prg  := "RSR001"
WCabec0 := 1
WCabec1 := OemtoAnsi(STR0008) //"Curriculo Vitae"
Contfl  := 1
Li      := 0
nTamanho:= "M"    
nOrdem  := aReturn[8]
l1Vez   := .T.
cCancel := STR0009 //"Cancelado pelo usuario"
nOrdem  := aReturn[8]
cArqNtx	:= ""

dbSelectArea("SQG")
If nOrdem == 1					// Curriculo
	dbSetOrder(1)
ElseIf nOrdem == 2			// Nome
	cArqNtx  := CriaTrab(NIL,.f.)
	cIndCond := "QG_FILIAL + QG_NOME"
	IndRegua("SQG",cArqNtx,cIndCond,,,STR0010) //"Selecionando Registros..."
EndIf

dbGoTop()

If nOrdem == 1
	dbSeek(xFilial("SQG")+mv_par01,.T.)
	cInicio := "SQG->QG_FILIAL + SQG->QG_CURRIC"
	cFim	:= xFilial("SQG")+mv_par02
ElseIf nOrdem == 2 
	dbSeek(xFilial("SQG")+Upper(mv_par03),.T.)
	cInicio := "SQG->QG_FILIAL + SQG->QG_NOME"
	cFim	:= xFilial("SQG")+mv_par04
EndIf

SetRegua( RecCount() )

While	!Eof() .And. &cInicio <= cFim

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimenta Regua Processamento                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	IncRegua()
	
	If 	( SQG->QG_CURRIC 		< mv_par01 			.Or. SQG->QG_CURRIC > mv_par02 ) 				.Or.;
		( Upper(SQG->QG_NOME) 	< Upper(mv_par03) 	.Or. Upper(SQG->QG_NOME) > Upper(mv_par04) ) 	.Or.;
		( SQG->QG_AREA 			< mv_par05 			.Or. SQG->QG_AREA > mv_par06 ) 					.Or.;
		( SQG->QG_SITUAC 		< mv_par07 			.Or. SQG->QG_SITUAC > mv_par08 )
		dbSkip()
		Loop
	EndIf	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cancela ImpressÆo ao se pressionar <ALT> + <A>               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lEnd
		cDetalhe:= STR0011 //"Cancelado pelo Operador"
		Impr(cDetalhe,"P")
		Exit
	EndIf

	// Dados Pessoais
	FDados()
	
	l1Vez:= .T.
	
	// Qualificacao 
	FQualific()

	l1Vez:= .T.
	// Experiencia
	FExperienc()

	l1Vez:= .T.	
	// Analise 
	FAnalise()

	l1Vez:= .T.	
	// Historico Profissional
	FHistor()

	l1Vez:= .T.
	cValid := "S"	
	// Formacao Escolar
	FEscolar()
	
	l1Vez:= .T.
	cValid := "N"
	// Cursos ExtraCurriculares
	FEscolar()
	
	dbSelectArea("SQG")
	DbSkip()

	If !Eof() .And. &cInicio <= cFim
		cDetalhe:=""
		Impr(cDetalhe,"P")
	EndIf

EndDo

cDetalhe:=""
Impr(cDetalhe,"F")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Termino do Relatorio                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SQG" )
dbSetOrder(1)
dbGoTop()

If File(cArqNtx+OrdBagExt())
	RetIndex("SQG")
	FErase(cArqNtx+OrdBagExt())
	Set Filter to
Endif

Set Device To Screen
Set Printer To
Commit

If aReturn[5] == 1
	Ourspool(wnrel)
EndIf

MS_FLUSH()
RETURN

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FDados   ³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de impressao dos Dados Pessoais do curriculo        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FDados()
cDetalhe:=""
Impr(cDetalhe,"C")
cDetalhe := Space(10)+OemtoAnsi(STR0012) //"DADOS PESSOAIS"
Impr(cDetalhe,"C")
cDetalhe:=""
Impr(cDetalhe,"C")
cDetalhe:= Space(15)+OemtoAnsi(STR0013)+ Alltrim(SQG->QG_NOME) //"Nome: "
Impr(cDetalhe,"C")
cDetalhe:= Space(15)+OemtoAnsi(STR0014) + Alltrim(SQG->QG_FONE) //"Tel.: "
Impr(cDetalhe,"C")

dbSelectArea("SX5")
dbSeek(xFilial()+"33"+SQG->QG_ESTCIV) 

cDetalhe:= Space(15)+Alltrim(RetTitle("QG_DTNASC"))+": " + DtoC(SQG->QG_DTNASC)+If(__SetCentury(),Space(3),Space(5))+OemtoAnsi(STR0016)+UPPER(Substr(SX5->X5_DESCRI,1,30)) //"Dt.Nasc.: "###"Est.Civil: "
Impr(cDetalhe,"C")
cDetalhe:= Space(15)+OemtoAnsi(STR0017)+ Alltrim(SQG->QG_ENDEREC) //"Endereco: "
Impr(cDetalhe,"C")
cDetalhe:= Space(25)+Alltrim(SQG->QG_COMPLEM)
Impr(cDetalhe,"C")
cDetalhe:= Space(25)+Alltrim(SQG->QG_BAIRRO)+" "+Alltrim(SQG->QG_MUNICIP)
Impr(cDetalhe,"C")
cDetalhe:= Space(25)+SQG->QG_ESTADO+"   "+SQG->QG_CEP
Impr(cDetalhe,"C")
cDetalhe:= Space(15)+Alltrim(RetTitle("QG_RG"))+": "+SQG->QG_RG+Space(05)+Alltrim(RetTitle("QG_CIC"))+": "+SQG->QG_CIC //"RG: "###"CIC: "
Impr(cDetalhe,"C")
cDetalhe:=""
Impr(cDetalhe,"C")

cDetalhe:= Space(10)+OemtoAnsi(STR0020) //"AREA DE INTERESSE"
Impr(cDetalhe,"C")
cDetalhe:=""
Impr(cDetalhe,"C")
cDetalhe:= Space(15)+OemtoAnsi(STR0021)+SQG->QG_DESCFUN+Space(05)+STR0022+str(SQG->QG_PRETSAL) //"Cargo: "###"Pret.Salarial: "
Impr(cDetalhe,"C")
cDetalhe:= Space(15)+OemtoAnsi(STR0023)+Alltrim(SQG->QG_INDICAD) //"Indicado por: "
Impr(cDetalhe,"C")
cDetalhe:=""
Impr(cDetalhe,"C")
RETURN 
	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FQualific³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime as Qualificacoes do curriculos                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/	
Static Function FQualific()
Local nLinha := 0
Local i		 := 0
Local cGrupo := ""
Local cFator := ""
Local cGrau  := ""
Local cDscFatAux := ""
Local cDscGraAux := ""

cSavAlias	:= Alias()
nSavReg  	:= RecNo()

cDetalhe:= Space(10)+OemtoAnsi(STR0024) //"QUALIFICACOES"
Impr(cDetalhe,"C")
cDetalhe:= ""
Impr(cDetalhe,"C")

dbSelectArea("SQI")
dbSetOrder(1)
If dbSeek(xFilial()+SQG->QG_CURRIC)
	cDetalhe:=Space(15)+OemtoAnsi(STR0040) //"Grupo              Fator                           Grau"
	Impr(cDetalhe,"C")	
	
	While !Eof() .And. xFilial("SQG")+SQG->QG_CURRIC == QI_FILIAL+QI_CURRIC
		cDescGru := TrmDesc("SQ0",SQI->QI_GRUPO,"Q0_DESCRIC")	             
		cDescFat := TrmDesc("SQ1",SQI->QI_GRUPO+SQI->QI_FATOR,"Q1_DESCSUM")
		cDescGra := TrmDesc("SQ2",SQI->QI_GRUPO+SQI->QI_FATOR+SQI->QI_GRAU,"Q2_DESC")
		
		cGrupo   := SQI->QI_GRUPO + "-"
		cFator   := SQI->QI_FATOR + "-"
		cGrau    := SQI->QI_GRAU  + "-"

        nLinha := Max( MlCount(cDescFat,30) , MlCount(cDescGra,30) ) //Verifica a quantidade de linhas para a maior descricao

        For i := 1 to nLinha
	        
			cDscFatAux := Memoline(cDescFat,30,i,,.T.)
			cDscGraAux := Memoline(cDescGra,30,i,,.T.)
			
			cDscFatAux := If (Empty(cDscFatAux),Space(30),cDscFatAux)
			cDscGraAux := If (Empty(cDscGraAux),Space(30),cDscGraAux)			
	
			cDetalhe:=Space(15)+cGrupo+cDescGru+" "+cFator+cDscFatAux+" "+cGrau+cDscGraAux
			
			cGrupo   := "   "
			cFator   := "   "
			cGrau    := "   "
			cDescGru := Space(15)

			Impr(cDetalhe,"C")		
        Next i
		
	    l1Vez := .F.
                                      
		dbSelectArea("SQI")
		dbSkip()
	EndDo
EndIf
If l1Vez
	cDetalhe:= Space(15)+ OemtoAnsi(STR0025) //"*** Qualificacoes nao cadastrada ***"
	Impr(cDetalhe,"C")
EndIf

cDetalhe:=""
Impr(cDetalhe,"C")

dbSelectArea(cSavAlias)
dbGoto(nSavReg)

RETURN
	  
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FExperienc³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime as Experiencias do curriculos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/	
Static Function FExperienc()
Local i	:= 0

cDetalhe:= Space(10)+OemtoAnsi(STR0038) //"EXPERIENCIA"
Impr(cDetalhe,"C")
cDetalhe:= ""
Impr(cDetalhe,"C")

cVar	:= MSMM(SQG->QG_EXPER,,,,3)
nLinha 	:= MlCount(cVar,80)
For i := 1 to nLinha
	cLine:= Space(15)+Memoline(cVar,80,i,,.T.)
	Impr(cLine,"C")
	l1Vez:= .F.
Next i  

If l1Vez
	cDetalhe:= Space(15)+ OemtoAnsi(STR0039) //"*** Experiencia nao cadastrada ***"
	Impr(cDetalhe,"C")
EndIf

cDetalhe:=""
Impr(cDetalhe,"C")

RETURN

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FAnalise ³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime a Analise feita do curriculo                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/	
Static Function FAnalise() 
Local i	:= 0
cDetalhe:= Space(10)+OemtoAnsi(STR0026) //"ANALISE"
Impr(cDetalhe,"C")
cDetalhe:= ""
Impr(cDetalhe,"C")

cVar	:= MSMM(SQG->QG_ANALISE,,,,3)
nLinha 	:= MlCount(cVar,80)
For i := 1 to nLinha 
	cLine:= Space(15)+MemoLine(cVar,80,i,,.T.)
	Impr(cLine,"C")
	l1Vez:= .F.
Next i	

If l1Vez
	cDetalhe:= Space(15)+ OemtoAnsi(STR0027) //"*** Analise nao cadastrada ***"
	Impr(cDetalhe,"C")
EndIf

cDetalhe:=""
Impr(cDetalhe,"C")

RETURN
	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FHistor  ³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Historico Profissional do candidato                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/	
Static Function FHistor()
Local i	:= 0

If li > 55
	cDetalhe:=""
	Impr(cDetalhe,"P")
EndIf

lAtiv:= .T.
cSavAlias:= Alias()
nSavReg  := RecNo()

cDetalhe:= Space(10)+OemtoAnsi(STR0028) //"HISTORICO PROFISSIONAL "
Impr(cDetalhe,"C")
cDetalhe:= ""
Impr(cDetalhe,"C")

dbSelectArea("SQL")
dbSetOrder(1)
dbSeek(xFilial()+SQG->QG_CURRIC)
While !Eof() .And. xFilial("SQG")+SQG->QG_CURRIC == QL_FILIAL+QL_CURRIC

	cDetalhe:=Space(15)+OemtoAnsi(STR0029) //"Admissao Demissao Area                                Empresa                        Funcao"
	//									    99/99/99 99/99/99  001 012345678901234567890123456789 012345678901234567890123456789 01234567890123456780 	
	Impr(cDetalhe,"C")	
	
	dbSelectArea("SX5")
	dbSetOrder(1)
	dbSeek(xFilial()+"R1"+SQL->QL_AREA)

	cDetalhe:=Space(15)+DtoC(SQL->QL_DTADMIS)+If(__SetCentury(),Space(1),Space(3))+DtoC(SQL->QL_DTDEMIS)+If(__SetCentury(),Space(1),Space(3))+SQL->QL_AREA+"-"+UPPER(Substr(SX5->X5_DESCRI,1,30))+"  "+SQL->QL_EMPRESA+" "+SQL->QL_FUNCAO
	Impr(cDetalhe,"C")		
	
	cDetalhe:= Space(17)+OemtoAnsi(STR0030) //"Atividades:"
	Impr(cDetalhe,"C")
	
	l1Vez:= .F.
	
	cVar	:= MSMM(SQL->QL_ATIVIDA,,,,3)
	nLinha 	:= MlCount(cVar,80)
	For i := 1 to nLinha
		cDetalhe:= Space(20)+MemoLine(cVar,80,i,,.T.)
		Impr(cDetalhe,"C")
		lAtiv := .F.
	Next i	
	
	If lAtiv
		cDetalhe:= Space(20)+ OemtoAnsi(STR0031) //"*** Atividade nao cadastrada ***"
		Impr(cDetalhe,"C")
	EndIf
	
	dbSelectArea("SQL")	
	dbSkip()
EndDo

If l1Vez
	cDetalhe:= Space(15)+ OemtoAnsi(STR0032) //"*** Historico Profissional nao cadastrada ***"
	Impr(cDetalhe,"C")
EndIf

cDetalhe:=""
Impr(cDetalhe,"C")

dbSelectArea(cSavAlias)
dbGoto(nSavReg)

RETURN 
	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FEscolar ³ Autor ³ Equipe R.H. 			³ Data ³ 03.03.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime a formacao Escolar e Cursos extracurriculares      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ cValid ="S" - Formacao Escolar  "N"-Curso ExtraCurricular  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ RdMake                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/	
Static Function FEscolar()

If li > 55
	cDetalhe:=""
	Impr(cDetalhe,"P")
EndIf

cSavAlias:= Alias()
nSavReg  := RecNo()

If cValid == "S"
	cDetalhe:= Space(10)+OemtoAnsi(STR0033) //"FORMACAO ESCOLAR"
	Impr(cDetalhe,"C")
Else
	cDetalhe:= Space(10)+OemtoAnsi(STR0034) //"CURSOS EXTRACURRICULARES"
	Impr(cDetalhe,"C")
EndIf	

cDetalhe:=""
Impr(cDetalhe,"C")

dbSelectArea("SQM")
dbSetOrder(1)
dbSeek(xFilial()+SQG->QG_CURRIC)

While !Eof() .And. xFilial()+SQG->QG_CURRIC == QM_FILIAL+QM_CURRIC

	If SQM->QM_ESCOLAR #cValid
		dbSkip()
		Loop
	EndIf
	
	If l1Vez
		cDetalhe:= Space(15)+OemtoAnsi(STR0035) //"Dt.Formacao Entidade                       Curso"
		//	    						    	     99/99/99   012345678901234567890123456789 012345 - 
		Impr(cDetalhe,"C")
		l1Vez:= .F.
	EndIf		


	dbSelectArea("SQT")
	dbSetOrder(1)
	dbSeek(xFilial()+SQM->QM_CURSO)
		
	cDetalhe:= Space(15)+ DtoC(SQM->QM_DATA)+If(__SetCentury(),Space(4),Space(6))+SQM->QM_ENTIDAD+" "+SQM->QM_CURSO+" - "+SQT->QT_DESCRIC
	Impr(cDetalhe,"C")	

	dbSelectArea("SQM")
	dbSetOrder(1)
	dbSkip()	
EndDo

If l1Vez
	If cValid == "S"
		cDetalhe:= Space(15)+ OemtoAnsi(STR0036) //"*** Formacao Escolar nao cadastrada ***"
	Else
		cDetalhe:= Space(15)+ OemtoAnsi(STR0037) //"*** Curso ExtraCurricular nao cadastrado ***"
	EndIf	
	Impr(cDetalhe,"C")
EndIf

cDetalhe:=""
Impr(cDetalhe,"C")

dbSelectArea(cSavAlias)
dbGoto(nSavReg)

RETURN
